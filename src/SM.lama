-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c, insns) {
  foldl (evalInsn, c, getBuffer (insns))
}

fun evalInsn (c, insn) {
  case insn of
    Read -> (
      var newWorld = readWorld (c[2]);
      [newWorld[0] : c[0], c[1], newWorld[1]]
    ) |
    Write -> [c[0].tl, c[1], writeWorld (c[0].hd, c[2])] |
    Binop (operator) -> [evalBinop (operator, c[0].tl.hd, c[0].hd) : c[0].tl.tl, c[1], c[2]] |
    LD (variable) -> [c[1] (variable) : c[0], c[1], c[2]] |
    ST (variable) -> [c[0].tl, c[1] <- [variable, c[0].hd], c[2]] |
    Const (number) -> [number : c[0], c[1], c[2]]
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Const (number) -> singletonBuffer (Const (number)) |
    Var (variable) -> singletonBuffer (LD (variable)) |
    Binop (operator, expr1, expr2) -> compileExpr (expr1) <+> compileExpr(expr2) <+> singletonBuffer (Binop (operator))
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  case stmt of
    Skip -> emptyBuffer() |
    Assn (variable, expr) -> compileExpr (expr) <+> singletonBuffer (ST (variable)) |
    Read (variable) -> singletonBuffer (Read) <+> singletonBuffer (ST (variable)) |
    Write (expr) -> compileExpr (expr) <+> singletonBuffer (Write) |
    Seq (stmt1, stmt2) -> compileSM (stmt1) <+> compileSM (stmt2)
  esac
}

